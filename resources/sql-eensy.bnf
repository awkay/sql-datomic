sql_data_statement ::=
             delete_statement
     |       select_statement
     |       insert_statement
     |       update_statement

delete_statement ::=
    <"DELETE"> <"FROM"> table_name
    [ wee_where_clause ]

insert_statement ::=
                 <"INSERT"> <"INTO"> table_name
                 <"("> insert_cols <")">
                 <"VALUES">
                 <"("> insert_vals <")">

select_statement ::= <"SELECT"> select_list from_clause [ wee_where_clause ]

update_statement ::=
    <"UPDATE"> table_name <"SET"> set_clausen
    [ wee_where_clause ]

table_name ::= #"[_A-Za-z]\w*"

wee_where_clause ::= <"WHERE">
                 ( something_between_dates
                 | something_compare_something )
                 [ { <"AND">
                     ( something_between_dates
                     | something_compare_something )
                   }+ ]

insert_cols ::= #"[_A-Za-z]\w*" [ { <","> #"[_A-Za-z]\w*" }+ ]

insert_vals ::=
                   ( character_string_literal
                   | exact_numeric_literal
                   | approximate_numeric_literal
                   | something_date_literal
                   | something_datetime_literal
                   | something_epochal_literal )
                   [ {
                     <",">
                     ( character_string_literal
                     | exact_numeric_literal
                     | approximate_numeric_literal
                     | something_date_literal
                     | something_datetime_literal
                     | something_epochal_literal )
                     }+ ]

from_clause ::=
    <"FROM"> table_reference
    [ { <","> table_reference }+ ]

select_list ::=
                ( column_name
                | qualified_asterisk
                | character_string_literal
                | exact_numeric_literal
                | approximate_numeric_literal )
                [ {
                  <",">
                  ( column_name
                  | qualified_asterisk
                  | character_string_literal
                  | exact_numeric_literal
                  | approximate_numeric_literal )
                  }+ ]

set_clausen ::= set_clause_assign_pair
            [ { <","> set_clause_assign_pair }+ ]

something_between_dates ::= column_name <"BETWEEN">
                        something_datetime <"AND"> something_datetime

something_compare_something ::= column_name
                            #'(=|!=|<>|<|<=|>|>=)'
                            something_value

approximate_numeric_literal ::=
             #"(\d+(\.(\d+)?)?|\.\d+)[eE](?:\+|-)?\d+"

character_string_literal ::= #"'[^']*'"

exact_numeric_literal ::= #"(\d+(\.(\d+)?)?|\.\d+)"

something_date_literal ::= <"date"> <"'"> #"\d{4}-\d{2}-\d{2}" <"'">

something_datetime_literal ::= <"datetime">
                           <"'">
                           #"\d{4}-\d{2}-\d{2}[ T]\d{2}:\d{2}:\d{2}"
                           <"'">

something_epochal_literal ::= <"time"> <"'"> #"\d+" <"'">

table_reference ::= table_name [ table_alias ]

column_name ::= #'("[^\"]+"|[_A-Za-z]\w*)'
                <"."> #'("[^\"]+"|[_A-Za-z]\w*)'

qualified_asterisk ::= #'("[^\"]+"|[_A-Za-z]\w*)' <"."> < asterisk >

set_clause_assign_pair ::=
                       column_name <"=">
                       ( character_string_literal
                       | exact_numeric_literal
                       | approximate_numeric_literal
                       | something_date_literal
                       | something_datetime_literal
                       | something_epochal_literal )

something_datetime ::= column_name
                   | something_date_literal
                   | something_datetime_literal
                   | something_epochal_literal

something_value ::= column_name
                | something_datetime
                | exact_numeric_literal
                | approximate_numeric_literal
                | character_string_literal

table_alias ::= [ <"AS"> ] #"'[^']+'|[_A-Za-z]\w*"

asterisk ::= "*"
