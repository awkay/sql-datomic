sql_data_statement ::= delete_statement
                     | select_statement
                     | insert_statement
                     | update_statement

delete_statement ::=
    <"DELETE"> <"FROM"> table_name
    [ where_clause ]

insert_statement ::=
                 <"INSERT"> <"INTO"> table_name
                 <"("> insert_cols <")">
                 <"VALUES">
                 <"("> insert_vals <")">

select_statement ::= <"SELECT"> select_list from_clause [ where_clause ]

update_statement ::=
    <"UPDATE"> table_name <"SET"> set_clausen
    [ where_clause ]

table_name ::= #"[_A-Za-z]\w*"

where_clause ::= <"WHERE">
                 ( between_clause
                 | binary_comparison )
                 [ { <"AND">
                     ( between_clause
                     | binary_comparison )
                   }+ ]

insert_cols ::= #"[_A-Za-z]\w*" [ { <","> #"[_A-Za-z]\w*" }+ ]

insert_vals ::= ( string_literal
                | boolean_literal
                | exact_numeric_literal
                | approximate_numeric_literal
                | date_literal
                | datetime_literal
                | epochal_literal )
                [ {
                  <",">
                  ( string_literal
                  | boolean_literal
                  | exact_numeric_literal
                  | approximate_numeric_literal
                  | date_literal
                  | datetime_literal
                  | epochal_literal )
                  }+ ]

from_clause ::= <"FROM"> table_ref [ { <","> table_ref }+ ]

select_list ::= ( column_name
                | qualified_asterisk
                | string_literal
                | boolean_literal
                | exact_numeric_literal
                | approximate_numeric_literal )
                [ {
                  <",">
                  ( column_name
                  | qualified_asterisk
                  | string_literal
                  | boolean_literal
                  | exact_numeric_literal
                  | approximate_numeric_literal )
                  }+ ]

set_clausen ::= assignment_pair [ { <","> assignment_pair }+ ]

between_clause ::= column_name <"BETWEEN">
                   chrono_value <"AND"> chrono_value

binary_comparison ::= column_name
                  {
                    ( #"(<=|<|>=|>)"
                      ( column_name
                      | chrono_value
                      | exact_numeric_literal
                      | approximate_numeric_literal )
                    )
                    |
                    ( #"(=|!=|<>)"
                      ( column_name
                      | boolean_literal
                      | chrono_value
                      | exact_numeric_literal
                      | approximate_numeric_literal
                      | string_literal )
                    )
                  }

string_literal ::= #"'(?:[^'\\]|\\.)*?'"

exact_numeric_literal ::= #"(\d+(\.(\d+)?)?|\.\d+)"

approximate_numeric_literal ::= #"(\d+(\.(\d+)?)?|\.\d+)[eE](?:\+|-)?\d+"

boolean_literal ::= true | false

true ::= <"TRUE">

false ::= <"FALSE">

date_literal ::= <"date"> <"'"> #"\d{4}-\d{2}-\d{2}" <"'">

datetime_literal ::= <"datetime">
                     <"'">
                     #"\d{4}-\d{2}-\d{2}[ T]\d{2}:\d{2}:\d{2}"
                     <"'">

epochal_literal ::= <"time"> <"'"> #"\d+" <"'">

table_ref ::= table_name [ table_alias ]

column_name ::= #'("[^\"]+"|[_A-Za-z]\w*)'
                <"."> #'("[^\"]+"|[_A-Za-z]\w*)'

qualified_asterisk ::= #'("[^\"]+"|[_A-Za-z]\w*)' <"."> <"*">

assignment_pair ::= column_name <"=">
                    ( string_literal
                    | boolean_literal
                    | exact_numeric_literal
                    | approximate_numeric_literal
                    | date_literal
                    | datetime_literal
                    | epochal_literal )

chrono_value ::= column_name
               | date_literal
               | datetime_literal
               | epochal_literal

table_alias ::= [ <"AS"> ] #'"[^\"]+"|[_A-Za-z]\w*'
