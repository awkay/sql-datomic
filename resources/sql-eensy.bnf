sql_data_statement ::= delete_statement
                     | select_statement
                     | insert_statement
                     | update_statement

delete_statement ::=
    <"DELETE"> <"FROM"> table_name
    [ where_clause ]

insert_statement ::=
                 <"INSERT"> <"INTO"> table_name
                 <"("> insert_cols <")">
                 <"VALUES">
                 <"("> insert_vals <")">

select_statement ::= <"SELECT"> select_list from_clause [ where_clause ]

update_statement ::=
    <"UPDATE"> table_name <"SET"> set_clausen
    [ where_clause ]

table_name ::= #"[-_A-Za-z][-\w]*"

where_clause ::= <"WHERE">
                 ( between_clause
                 | binary_comparison )
                 [ { <"AND">
                     ( between_clause
                     | binary_comparison )
                   }+ ]

insert_cols ::= #"[-_A-Za-z][-\w]*" [ { <","> #"[-_A-Za-z][-\w]*" }+ ]

insert_vals ::= ( string_literal
                | boolean_literal
                | long_literal
                | bigint_literal
                | double_literal
                | float_literal
                | bigdec_literal
                | keyword_literal
                | date_literal
                | datetime_literal
                | epochal_literal
                | inst_literal
                | uuid_literal
                | uri_literal
                | bytes_literal )
                [ {
                  <",">
                  ( string_literal
                  | boolean_literal
                  | long_literal
                  | bigint_literal
                  | double_literal
                  | float_literal
                  | bigdec_literal
                  | keyword_literal
                  | date_literal
                  | datetime_literal
                  | epochal_literal
                  | inst_literal
                  | uuid_literal
                  | uri_literal
                  | bytes_literal )
                  }+ ]

from_clause ::= <"FROM"> table_ref [ { <","> table_ref }+ ]

select_list ::= ( column_name
                | qualified_asterisk
                | string_literal
                | boolean_literal
                | date_literal
                | datetime_literal
                | epochal_literal
                | inst_literal
                | long_literal
                | bigint_literal
                | double_literal
                | float_literal
                | bigdec_literal
                | uuid_literal
                | uri_literal
                | bytes_literal )
                [ {
                  <",">
                  ( column_name
                  | qualified_asterisk
                  | string_literal
                  | boolean_literal
                  | date_literal
                  | datetime_literal
                  | epochal_literal
                  | inst_literal
                  | long_literal
                  | bigint_literal
                  | double_literal
                  | float_literal
                  | bigdec_literal
                  | uuid_literal
                  | uri_literal
                  | bytes_literal )
                  }+ ]

set_clausen ::= assignment_pair [ { <","> assignment_pair }+ ]

between_clause ::= column_name <"BETWEEN">
               {
                 (
                   ( date_literal | datetime_literal | epochal_literal
                   | inst_literal )
                   <"AND">
                   ( date_literal | datetime_literal | epochal_literal
                   | inst_literal )
                 )
                 |
                 (
                   ( long_literal
                   | bigint_literal
                   | double_literal
                   | float_literal
                   | bigdec_literal
                   | uuid_literal )
                   <"AND">
                   ( long_literal
                   | bigint_literal
                   | double_literal
                   | float_literal
                   | bigdec_literal
                   | uuid_literal )
                 )
               }

binary_comparison ::= column_name
                  {
                    ( #"(<=|<|>=|>)"
                      ( column_name
                      | date_literal
                      | datetime_literal
                      | epochal_literal
                      | inst_literal
                      | long_literal
                      | bigint_literal
                      | double_literal
                      | float_literal
                      | bigdec_literal
                      | uuid_literal ) )
                    |
                    ( #"(=|!=|<>)"
                      ( column_name
                      | keyword_literal
                      | boolean_literal
                      | date_literal
                      | datetime_literal
                      | epochal_literal
                      | inst_literal
                      | long_literal
                      | bigint_literal
                      | double_literal
                      | float_literal
                      | bigdec_literal
                      | string_literal
                      | uuid_literal
                      | uri_literal ) )
                  }

string_literal ::= #"'(?:[^'\\]|\\.)*?'"

long_literal ::= #"(?:-|\+)?(?:0|[1-9]\d*)"

bigint_literal ::= #"(?:-|\+)?(?:0|[1-9]\d*)N"

double_literal ::= #"(?:(?i)[-+]?(?:\d(?:\.\d*)?[Ee][-+]?\d+|\d+\.\d*))"

float_literal ::= #"(?:(?i)[-+]?(?:\d(?:\.\d*)?[Ee][-+]?\d+|\d+\.\d*))[Ff]"
    | <"#float"> #"(?:(?i)[-+]?(?:\d(?:\.\d*)?[Ee][-+]?\d+|\d+\.\d*))"

bigdec_literal ::= #"(?:(?i)[-+]?(?:\d(?:\.\d*)?[Ee][-+]?\d+|\d+\.\d*))M"

boolean_literal ::= true | false

true ::= <"TRUE">

false ::= <"FALSE">

inst_literal ::= <"#inst">
     <"\"">
     #"\d{4}-\d{2}-\d{2}(T\d{2}:\d{2}:\d{2}(\.\d{3})?([-\+]\d{2}:\d{2})?)?"
     <"\"">

date_literal ::= <"date"> <"'"> #"\d{4}-\d{2}-\d{2}" <"'">

datetime_literal ::= <"datetime">
                     <"'">
                     #"\d{4}-\d{2}-\d{2}[ T]\d{2}:\d{2}:\d{2}"
                     <"'">

epochal_literal ::= <"time"> <"'"> #"\d+" <"'">

keyword_literal ::= <":"> #"[-\.\*\+\$\?\|_/<>=%!#\w]+"

uuid_literal ::= <"#uuid">
     <"\"">
     #"[\da-f]{8}-[\da-f]{4}-[\da-f]{4}-[\da-f]{4}-[\da-f]{12}"
     <"\"">

uri_literal ::= <"#uri"> <"\""> #"[^\"]+" <"\"">

bytes_literal ::= <#"#(bytes|base64)"> <"\""> #"[A-Za-z0-9+/=]+" <"\"">

table_ref ::= table_name [ table_alias ]

column_name ::= #'("[^\"]+"|[-_A-Za-z][-\w]*)'
                <"."> #'("[^\"]+"|[-_A-Za-z][-\w]*)'
              | <":">
                #"([-\.\*\+\$\?\|_<>=%!#\w]+)"
                <"/">
                #"([-\.\*\+\$\?\|_<>=%!#\w]+)"

qualified_asterisk ::= #'("[^\"]+"|[-_A-Za-z][-\w]*)' <"."> <"*">

assignment_pair ::= column_name <"=">
                    ( string_literal
                    | boolean_literal
                    | long_literal
                    | bigint_literal
                    | double_literal
                    | float_literal
                    | bigdec_literal
                    | keyword_literal
                    | date_literal
                    | datetime_literal
                    | epochal_literal
                    | inst_literal
                    | uuid_literal
                    | uri_literal
                    | bytes_literal )

table_alias ::= [ <"AS"> ] #'"[^\"]+"|[-_A-Za-z][-\w]*'

(* TODO:
- support IN clauses in WHERE
- support OR in WHERE
- support NOT (???)
*)
