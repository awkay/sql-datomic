update_statement ::=
    <"UPDATE"> table_name <"SET"> set_clausen
    [ wee_where_clause ]

set_clausen ::= set_clause_assign_pair
            [ { <","> set_clause_assign_pair }+ ]

table_name ::= #"[_A-Za-z]\w*"

wee_where_clause ::= <"WHERE">
                 ( something_between_dates
                 | something_compare_something )
                 [ { <"AND">
                     ( something_between_dates
                     | something_compare_something )
                   }+ ]

set_clause_assign_pair ::=
                       column_name <"=">
                       ( character_string_literal
                       | exact_numeric_literal
                       | approximate_numeric_literal
                       | something_date_literal
                       | something_datetime_literal
                       | something_epochal_literal )

something_between_dates ::= column_name <"BETWEEN">
                        something_datetime <"AND"> something_datetime

something_compare_something ::= column_name
                            #'(=|!=|<>|<|<=|>|>=)'
                            something_value

approximate_numeric_literal ::=
             #"(\d+(\.(\d+)?)?|\.\d+)[eE](?:\+|-)?\d+"

character_string_literal ::= #"'[^']*'"

column_name ::= #'("[^\"]+"|[_A-Za-z]\w*)'
                <"."> #'("[^\"]+"|[_A-Za-z]\w*)'

exact_numeric_literal ::= #"(\d+(\.(\d+)?)?|\.\d+)"

something_date_literal ::= <"date"> <"'"> #"\d{4}-\d{2}-\d{2}" <"'">

something_datetime_literal ::= <"datetime">
                           <"'">
                           #"\d{4}-\d{2}-\d{2}[ T]\d{2}:\d{2}:\d{2}"
                           <"'">

something_epochal_literal ::= <"time"> <"'"> #"\d+" <"'">

something_datetime ::= column_name
                   | something_date_literal
                   | something_datetime_literal
                   | something_epochal_literal

something_value ::= column_name
                | something_datetime
                | exact_numeric_literal
                | approximate_numeric_literal
                | character_string_literal

